plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.protobuf' version "0.8.17"
    id 'kotlin-kapt'
    id "kotlin-parcelize"
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
}
apply from: "${rootProject.projectDir}/flavors.gradle"

Properties properties = new Properties()
if (project.rootProject.file('local.properties').canRead()) {
    properties.load(project.rootProject.file("local.properties").newDataInputStream())
}

android {
    compileSdkVersion 31

    signingConfigs {
        release {
            storeFile file(properties['STORE_FILE'])
            storePassword properties['STORE_PASSWORD']
            keyAlias properties['KEY_ALIAS']
            keyPassword properties['KEY_PASSWORD']
        }
    }

    defaultConfig {
        applicationId "com.indaco.samples"
        minSdkVersion 26
        targetSdkVersion 31
        versionName = "1.1"
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }

    with flavorConfig

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    dynamicFeatures = [
            ':features:coffee',
            ':features:goals',
            ':features:news',
            ':features:bart',
            ':features:camera',
            ':features:hue',
            ':features:home',
            ':instant:auth',
            ':instant:promo'
    ]
}

dependencies {
    // Core
    api 'androidx.appcompat:appcompat:1.3.1'
    api 'androidx.constraintlayout:constraintlayout:2.1.1'
    api "androidx.core:core-ktx:1.6.0"
    api 'com.google.android.material:material:1.4.0'
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // Datastore
    api "androidx.datastore:datastore:1.0.0"
    api "androidx.datastore:datastore-preferences:1.0.0"
    api 'androidx.fragment:fragment-ktx:1.3.6'
    api 'androidx.legacy:legacy-support-v4:1.0.0'

    // Lifecycle
    api 'androidx.lifecycle:lifecycle-common-java8:2.3.1'
    api 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    api "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    api "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    // Navigation
    api "androidx.navigation:navigation-fragment-ktx:$nav_version"
    api "androidx.navigation:navigation-ui-ktx:$nav_version"
    api "androidx.navigation:navigation-dynamic-features-fragment:$nav_version"

    api 'androidx.preference:preference-ktx:1.1.1'

    // Room
    api "androidx.room:room-ktx:$room_version"
    api "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    api 'com.google.android.gms:play-services-instantapps:17.0.1'

    // Hilt
    api "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Protobuf
    api "com.google.protobuf:protobuf-javalite:$protobufVersion"

    // Retrofit
    api 'com.squareup.okhttp3:okhttp:5.0.0-alpha.2'
    api 'com.squareup.retrofit2:converter-gson:2.9.0'
    api 'com.squareup.retrofit2:retrofit:2.9.0'

}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.15.6"
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}
