plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.google.protobuf' version "0.8.12"
    id 'kotlin-kapt'
    id "kotlin-parcelize"
    id 'dagger.hilt.android.plugin'
}

def getBartKey = { ->
    try {
        def apikeyPropertiesFile = rootProject.file("apikeys.properties")
        def apikeyProperties = new Properties()
        apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))
        return apikeyProperties['BART_PRIVATE_KEY']
    } catch (ignored) {
        return "\"MW9S-E7SL-26DU-VV8V\""
    }
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 26
        targetSdk 31
        versionCode 1
        versionName "1.0"

        buildConfigField("String", "BART_KEY", getBartKey())

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    api 'androidx.core:core-ktx:1.6.0'
    api 'androidx.appcompat:appcompat:1.3.1'
    api 'com.google.android.material:material:1.4.0'

    // DataStore
    api "androidx.datastore:datastore:1.0.0"
    api "androidx.datastore:datastore-preferences:1.0.0"
    api 'androidx.preference:preference-ktx:1.1.1'
    api "com.google.protobuf:protobuf-javalite:$protobufVersion"

    // Hilt
    api "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Retrofit
    api 'com.squareup.retrofit2:retrofit:2.9.0'
    api 'com.squareup.retrofit2:converter-gson:2.9.0'

    // Room
    api "androidx.room:room-ktx:$room_version"
    api "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.15.6"
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}
